%import common.NUMBER -> UNSIGNED_NUMBER
%import common.INT -> _INT
%import common (WORD, WS)
%ignore WS

// rules

start: iota

?iota: _non_pattern_iota | pattern
_non_pattern_iota: list
                 | vector
                 | matrix
                 | NUMBER
                 | BOOLEAN
                 | NULL
                 | STRING

// note: HexGloop skips commas between consecutive patterns in latest.log
list: "[" _delimited{_list_item, ","}? "]"
_list_item: _non_pattern_iota | pattern+

pattern: "HexPattern" "(" DIRECTION ANGLES? ")"
       | "<" DIRECTION ","? ANGLES? ">"

vector: "(" NUMBER "," NUMBER "," NUMBER ")"

matrix.1: "[" "(" INT "," INT ")" ( "|" matrix_rows )? "]"
matrix_rows: _delimited{matrix_row, ";"} -> list
matrix_row: _delimited{NUMBER, ","} -> list

// terminals

DIRECTION: ( ( "north"i | "south"i ) "_"? )? ( "west"i | "east"i )
ANGLES: /[aqweds]+/i

NUMBER: "-"? UNSIGNED_NUMBER
INT.1: _INT

BOOLEAN: "true"i | "false"i
NULL: "null"i

STRING: "\"" /[^"]*/ "\""

// templates

_delimited{value, separator}: value ( separator value )*
